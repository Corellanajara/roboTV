import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    defaultConfig {
        applicationId "org.xvdr.robotv"
        minSdkVersion 22
        targetSdkVersion 24
        versionCode 1
        versionName "0.5.2.${gitAbbreviatedId}"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            archivesBaseName = "robotv-" + gitBranch + "-" + gitAbbreviatedId
        }
        debug {
            archivesBaseName = "robotv"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    task ndkBuild(type: Exec) {
        def ndkDir = project.plugins.findPlugin('com.android.application').sdkHandler.getNdkFolder()
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine "$ndkDir/ndk-build.cmd", '-C', file("$projectDir").absolutePath
        } else {
            commandLine "$ndkDir/ndk-build", '-C', file("$projectDir").absolutePath
        }
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}

dependencies {
    provided fileTree(include: ['*.so'], dir: 'libs')
    compile 'com.android.support:leanback-v17:24.2.0'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.10'
}
